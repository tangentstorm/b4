#!/bin/env lilt
# ------------------------------------------
# the b4 virtual machine, implemented in Lil
# ------------------------------------------

vm.ip: 256
vm.ds: ()
vm.cs: ()

on dpop do r: last vm.ds vm.ds:-1 drop vm.ds r end
on dput x do vm.ds: vm.ds,x end

op.ad: on ad do dput[dpop[] + dpop[]] end
op.ml: on ml do dput[dpop[] * dpop[]] end
op.sb: on sb do y:dpop[] dput[dpop[]-y] end
op.dv: on dv do y:dpop[] dput[floor dpop[]/y] end
op.md: on md do dput[dpop[] % dpop[]] end # !!
op.sh: on sh do y:2^dpop[] dput[dpop[]*y] end
op.an: on an do dput[bits.and[dpop[] dpop[]]] end
op.or: on or do dput[bits.or[dpop[] dpop[]]] end
op.xr: on xr do dput[bits.xor[dpop[] dpop[]]] end
op.nt: on nt do dput[-1-dpop[]] end
op.zp: on zp do dpop[] end


on toHex n do
  if n < 0 "-%H" format -n else "%H" format n end
end
on isHex s do
  min each c in s c in "0123456789ABCDEF" end
end
on unHex s do "%H" parse s end
on ord c do "%a" parse c end
on fmt xs do
  "%s: [%s]" format xs,(" " fuse toHex @ vm[xs])
end

on b4i cmd do
  if     cmd="%q" exit[0]
  elseif cmd="?d" print[fmt["ds"]]
  elseif cmd="?c" print[fmt["cs"]]
  elseif cmd="?i" print["ip: %02H" format vm.ip]
  elseif cmd="'" vm.ds: vm.ds,32
  elseif cmd[0]="'" vm.ds: vm.ds,ord[cmd[1]]
  elseif cmd[0]="`" vm.ds: vm.ds,4*ord[cmd[1]]-64
  elseif isHex[cmd] vm.ds: vm.ds,unHex[cmd]
  elseif cmd in op op[cmd][]
  else print["no: %s" format cmd] end
end

shell["sleep 0.01"]
on main do
  each line in "\n" split read["input.txt"]
    b4i @ " " split line
  end
end

main[]
